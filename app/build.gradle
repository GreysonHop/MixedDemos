apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "com.testdemo"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        renderscriptTargetApi 18
        renderscriptSupportModeEnabled true

        multiDexEnabled true

        resValue "string", "google_search_api_key", (project.findProperty("GOOGLE_SEARCH_API_KEY") ?: "")
        resValue "string", "google_map_api_key", (project.findProperty("GOOGLE_MAP_API_KEY") ?: "")

        manifestPlaceholders=[UMENG_CHANNEL_VALUE:'Default'] // 默认渠道为Default
    }

    signingConfigs {
        release {
            keyAlias 'testdemo'
            keyPassword 'testdemokey'
            storeFile file('../testKeyStore.jks')
            storePassword 'testdemopwd'
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            debuggable false
            zipAlignEnabled true

            minifyEnabled true
            shrinkResources true

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            signingConfig signingConfigs.release
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        /**
         * The `initWith` property allows you to copy configurations from other build types,
         * then configure only the settings you want to change. This one copies the debug build
         * type, and then changes the manifest placeholder and application ID.
         */
        staging {
            initWith debug
            manifestPlaceholders = [hostName:"internal.example.com"]
            applicationIdSuffix ".debugStaging"
        }

        applicationVariants.all { variant ->
            variant.outputs.all {
                if (variant.buildType.name == "debug") {
                    outputFileName = outputFile.name.replace(
                            ".apk",
                            "-v${defaultConfig.versionName}" + ".apk")
                } else if (variant.buildType.name == "release") {
                    outputFileName = "TestDemo_V${variant.versionName}" +
                            "_${new Date().format("yyyyMMdd")}_release.apk"
                }
            }
        }
    }

    //测试多渠道打包，参考官网：https://developer.android.com/studio/build/build-variants
    flavorDimensions "version"//, "test"
    productFlavors {
        greyson {
            dimension "version"
            manifestPlaceholders=[UMENG_CHANNEL_VALUE:'Greyson'] //博客者应该是用了友盟的渠道统计SDK
        }
        demo {
            manifestPlaceholders=[UMENG_CHANNEL_VALUE:'Demo']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        //noinspection DataBindingWithoutKapt
        enabled = true
    }
}

repositories {
    mavenCentral()
    mavenLocal() //Add local m2 repository of yours
    jcenter()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //跟创建项目时自带的配置一样
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

//    androidTestImplementation files('libs/httpunit-1.7.jar')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'

    /*IdeMavenCoordinates{myGroupId='com.android.support'; myArtifactId='appcompat-v7'
        ; myVersion='24.2.1'; myPacking='aar'; myClassifier='null'}*/

    implementation files('libs/brokenview.jar')
    implementation 'com.wonderkiln:blurkit:1.0.0'
    implementation 'com.jcodecraeer:xrecyclerview:1.5.9'

    implementation 'com.lcodecorex:tkrefreshlayout:1.0.7'
    implementation 'com.github.LuckSiege.PictureSelector:picture_library:v2.0.0'
    implementation 'com.github.yyued:SVGAPlayer-Android:2.1.8'
    implementation 'com.squareup.okhttp3:okhttp:3.4.1'

//    implementation 'com.github.GreysonHop:WeChatTag:v2.0.0'
    implementation project(path: ':ncalendar')

    implementation 'com.google.android.libraries.places:places:2.1.0'
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
//    地图操作工具类，添加标记等
//    implementation 'com.google.maps.android:android-maps-utils:0.5+'

    implementation 'androidx.multidex:multidex:2.0.1'
    api 'io.reactivex.rxjava2:rxjava:2.2.5'
    api 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.50'

    implementation 'com.akexorcist:round-corner-progress-bar:2.1.0'
    implementation 'com.facebook.rebound:rebound:0.3.8'
}
